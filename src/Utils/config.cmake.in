# Dependencies
include(CMakeFindDependencyMacro)
if(NOT TARGET Boost::log)
  set(Boost_USE_STATIC_LIBS OFF)
  unset(Boost_FOUND)
  find_dependency(Boost REQUIRED COMPONENTS log)
endif()
find_dependency(Eigen3 3.3.1 REQUIRED NO_MODULE)
find_dependency(yaml-cpp REQUIRED)
# If OpenMP was linked find it
if (@OpenMP_CXX_FOUND@)
  find_dependency(OpenMP)
endif()
# We are interface-dependent on Core, so those headers must also be available
find_dependency(Scine REQUIRED COMPONENTS Core)

if(DEFINED SCINE_MARCH AND NOT "${SCINE_MARCH}" STREQUAL "@SCINE_MARCH@")
  message(FATAL_ERROR "You are trying to use an installed version of the SCINE Utils that was compiled with different -march flag values! Current march flags: ${SCINE_MARCH}, installed flags: @SCINE_MARCH@")
endif()
if (NOT DEFINED SCINE_MARCH)
  set(SCINE_MARCH "@SCINE_MARCH@")
endif()

include(${CMAKE_CURRENT_LIST_DIR}/UtilsOSTargets.cmake)

@PACKAGE_INIT@

check_required_components(UtilsOS)
